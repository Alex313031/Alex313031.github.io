<!DOCTYPE html>
<html><head><meta charset='utf-8' />
<title>wireframe hqz preview</title>
<script type='text/javascript'>
    /* http://www.trajanmcgill.com/stronglytyped/post/2013/03/23/A-More-Robust-requestAnimationFrame-Polyfill */
    /*! requestAnimationFrame.js */
    (function(){"use strict";function e(e){var t=a;return a++,l[t]=e,c++,null===r&&(r=
    window.setTimeout(i,Math.max(0,16-((new Date).getTime()-w)))),t}function t(e){e>=s
    &&l.hasOwnProperty(e)?n(l,e):o.hasOwnProperty(e)&&n(o,e)}function n(e,t){delete e[
    t],c--,1>c&&null!==r&&(window.clearTimeout(r),r=null)}function i(){var e,t;r=null,w
    =(new Date).getTime(),o=l,l={},s=a;for(e in o)if(o.hasOwnProperty(e)){t=o[e],delete
    o[e],c--;try{t(w)}catch(n){}}}var u,a=0,l={},o={},r=null,w=0,c=0,s=0,m=window,f=
    "AnimationFrame",h="cancel"+f,d="request"+f,q=["webkitCancel","webkitCancelRequest",
    "mozCancel"],b=["webkitRequest","webkitRequest","mozRequest"];for(u=0;!m[h]&&q.length
    >u;u++)m[h]=m[q[u]+f],m[d]=m[b[u]+f];m[h]||(m[h]=t,m[d]=e)})();
</script>
<script type='text/coffeescript'>

# This CoffeeScript is compiled in the browser by the compiler we include below.
# This is a convenience so that this file runs right out of git, without any build steps.

@status = 'Drop .json file here'
@aspect = 16/9
@fps = 30
@frameRequested = false
@lines = []

$(window)
    .bind('dragenter dragover', false)
    .bind 'drop', (e) =>
        e.stopPropagation()
        e.preventDefault()
        files = e.target.files or e.originalEvent.dataTransfer.files

        # Trash old file first
        @lines = []

        @status = 'Loading...'
        @redraw()

        if files.length != 1
            @status = 'Drop exactly one file'
            return @redraw()

        reader = new FileReader
        reader.onload = (e) =>
            try
                @fileLoaded e.target.result
            catch error
                @status = error
            @redraw()

        reader.readAsText files[0] 

@resize = () ->
    @width = window.innerWidth
    @height = window.innerHeight
    if @width / @height > @aspect
        @width = @height * @aspect
    else
        @height = @width / @aspect

    $('#preview').css
        width: @width
        height: @height
        'margin-left': -@width/2
        'margin-top': -@height/2

    @canvas = document.getElementById 'preview'
    @canvas.width = @width
    @canvas.height = @height
    @ctx = @canvas.getContext '2d'
    @redraw()

@redraw = () ->
    @ctx.fillStyle = '#111'
    @ctx.fillRect 0, 0, @width, @height

    if @lines.length
        now = new Date()
        frame = ((now - @beginTime) * @fps / 1000) | 0
        if frame >= @lines.length
            frame = 0
            @beginTime = now

        scene = JSON.parse @lines[frame]
        @drawFrame scene

        @ctx.font = "12px sans-serif"
        @ctx.textAlign = 'left'
        @ctx.textBaseline = 'top'

        @ctx.fillStyle = '#ddd'
        @ctx.fillText "Frame #{frame}, #{(frame / @fps)|0} sec", 5, 5

        @ctx.fillStyle = '#888'
        @ctx.fillText "Exposure: #{ scene.exposure }", 5, 30
        @ctx.fillText "Rays: #{ scene.rays }", 5, 45
        @ctx.fillText "Objects: #{ scene.objects.length }", 5, 60

        # Do we need to animate?
        if @lines.length > 1 and not @frameRequested
            @frameRequested = true
            window.requestAnimationFrame () =>
                @frameRequested = false
                @redraw()

    else
        # Nothing playing; display a name badge

        @ctx.font = "#{@width * 0.07}px courier"
        @ctx.textAlign = 'center'
        @ctx.textBaseline = 'top'
        @ctx.fillStyle = '#444'
        @ctx.fillText "wireframe hqz preview", @width * 0.5, 0

    if @status
        @ctx.font = "20px sans-serif"
        @ctx.textAlign = 'center'
        @ctx.textBaseline = 'middle'
        @ctx.fillStyle = '#888'
        @ctx.fillText @status, @width * 0.5, @height * 0.5

@sampleCenter = (obj) ->
    # Sample a 'typical' value for the random variable 'obj'
    return (obj[0] + obj[1]) / 2 if obj.length == 2
    return obj

@fileLoaded = (data) ->
    lines = data.trim().split '\n'
    firstFrame = JSON.parse lines[0]
    res = firstFrame.resolution

    @beginTime = new Date()
    @status = ''
    @aspect = sampleCenter(res[0]) / sampleCenter(res[1])
    @lines = lines
    @resize()

@transform = (scene, x, y) ->
    vx = @sampleCenter scene.viewport[0]
    vy = @sampleCenter scene.viewport[1]
    vw = @sampleCenter scene.viewport[2]
    vh = @sampleCenter scene.viewport[3]
    [ (x - vx) / vw * @width, (y - vy) / vh * @height]

@drawFrame = (scene) ->
    @ctx.lineWidth = 1.0
    @ctx.strokeStyle = '#faf'

    for obj in scene.lights
        x = @sampleCenter obj[1]
        y = @sampleCenter obj[2]
        angle = Math.PI / 180 * @sampleCenter obj[5]
        [x, y] = @transform scene, x, y
        @ctx.beginPath()
        @ctx.arc x, y, 10, angle + 0, angle + 2 * Math.PI, false
        @ctx.arc x, y, 40, angle + 0, angle + 2 * Math.PI, false
        @ctx.stroke()

    @ctx.lineWidth = 0.75
    @ctx.strokeStyle = '#fff'

    for obj in scene.objects
        switch obj.length
            when 5
                # Plain old line segment

                x0 = @sampleCenter obj[1]
                y0 = @sampleCenter obj[2]
                dx = @sampleCenter obj[3]
                dy = @sampleCenter obj[4]

                t0 = @transform scene, x0, y0
                t1 = @transform scene, x0+dx, y0+dy

                @ctx.beginPath()
                @ctx.moveTo t0[0], t0[1]
                @ctx.lineTo t1[0], t1[1]
                @ctx.stroke()

            when 7
                # Line segment with trigonometrically interpolated normals.
                # Draw the normals in the conventional way as short lines
                # sticking out from the surface, but indicate the delta with an
                # arc extending tangent to this line. This makes surfaces look
                # "hairy", and each hair has a short "tooth" pointing along
                # the interpolation path.

                x0 = @sampleCenter obj[1]
                y0 = @sampleCenter obj[2]
                a0 = Math.PI / 180 * @sampleCenter obj[3]
                dx = @sampleCenter obj[4]
                dy = @sampleCenter obj[5]
                da = Math.PI / 180 * @sampleCenter obj[6]

                t0 = @transform scene, x0, y0
                t1 = @transform scene, x0+dx, y0+dy

                @ctx.beginPath()
                @ctx.moveTo t0[0], t0[1]
                @ctx.lineTo t1[0], t1[1]
                @ctx.stroke()

                @ctx.beginPath()
                @ctx.moveTo t0[0], t0[1]
                @ctx.arc t0[0], t0[1], 10, a0, a0 + da, da < 0
                @ctx.strokeStyle = '#44f'
                @ctx.stroke()
                @ctx.strokeStyle = '#fff'

            else
                console.log [ "Unsupported object", obj ]


$(window).resize () => @resize()
$(document).ready () => @resize()

</script>
<script type='text/javascript' src='http://ajax.googleapis.com/ajax/libs/jquery/1.9.1/jquery.min.js'></script>
<script type='text/javascript' src='http://coffeescript.org/extras/coffee-script.js'></script>
<style>

body {
    background: #333;
    overflow: hidden;
    height: 100%;
    margin: 0;
    padding: 0;
}

#preview {
    display: block;
    position: absolute;
    top: 50%;
    left: 50%;
}

</style></head>
<body><canvas id='preview'></canvas></body>
</html>
